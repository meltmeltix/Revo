plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jlleitschuh.gradle.ktlint' version "11.0.0"
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.alessiocameroni.revomusicplayer"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "0.1 Â· Amber"
        resourceConfigurations += ['en', 'it', 'de']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion kotlin_compiler_extension_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'com.alessiocameroni.revomusicplayer'
}

ktlint {
    android = true
    ignoreFailures = false
    reporters {
        reporter "plain"
        reporter "checkstyle"
        reporter "sarif"
    }
}

dependencies {
    // App core
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.activity:activity-compose:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.6.1'
    implementation "androidx.compose.runtime:runtime-livedata:1.5.0-alpha02"
    implementation 'androidx.core:core-splashscreen:1.0.0'

    // Component libraries
    implementation 'androidx.compose.material3:material3:1.1.0-beta02'
    implementation 'androidx.compose.material3:material3-window-size-class:1.1.0-beta02'
    implementation 'com.github.alessiocameroni:pixely-components:2.1.5'

    // Dependency Injection
    implementation 'com.google.dagger:hilt-android:2.45'
    kapt 'com.google.dagger:hilt-compiler:2.45'
    implementation 'androidx.hilt:hilt-navigation-compose:1.1.0-alpha01'

    // Accompanist
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    // Database libraries
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Image processing
    implementation('io.coil-kt:coil-compose:2.3.0')

    // Media player
    implementation 'com.google.android.exoplayer:exoplayer:2.18.5'

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation files('lib/spotify-app-remote-release-0.7.2.aar')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}

kapt {
    correctErrorTypes true
}